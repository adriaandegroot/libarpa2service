Domain-or-Network-Access-Identifier Grammar
===========================================

DoNAI user formal syntax.

The grammar for the user is given below, described in Augmented
Backus-Naur Form (ABNF) as documented in [RFC5234].

donai-user     = user /
                 service
user           = dot-string /
                 dot-string alias /
                 dot-string signature
alias          = "+" dot-string
signature      = "+" sigtmpl "+" [dot-string] "+"
sigtmpl        = dot-string
service        = "+" plus-string
plus-string    = dot-string *("+" dot-string)
dot-string     = string *("." string)
string         = 1*atext
atext          = ALPHA / DIGIT /
                 "!" / "#" /
                 "$" / "%" /
                 "&" / "'" /
                 "*" /
                 "-" / "/" /
                 "=" / "?" /
                 "^" / "_" /
                 "`" / "{" /
                 "|" / "}" /
                 "~" /
                 UTF8-xtra-char

UTF8-xtra-char = UTF8-2 / UTF8-3 / UTF8-4

UTF8-2         = %xC2-DF UTF8-tail

UTF8-3         = %xE0 %xA0-BF UTF8-tail /
                 %xE1-EC 2( UTF8-tail ) /
                 %xED %x80-9F UTF8-tail /
                 %xEE-EF 2( UTF8-tail )

UTF8-4         = %xF0 %x90-BF 2( UTF8-tail ) /
                 %xF1-F3 3( UTF8-tail ) /
                 %xF4 %x80-8F 2( UTF8-tail )

UTF8-tail      = %x80-BF
