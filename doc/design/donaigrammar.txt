Domain-or-Network-Access-Identifier Grammar
===========================================

ABNF syntax of DoNAI, which is loosely based on the NAI syntax as specified by
RFC 4282.

grammar     = donai
donai       =  domain
donai       =/ "@" domain
donai       =/ username "@" domain
username    =  dot-string
dot-string  =  string
dot-string  =/ dot-string "." string
string      =  char
string      =/ string char

char        =  c
char        =/ "\" x
c           =  %x21    ; !
c           =/ %x23-27 ; # $ % & '
c           =/ %x2A-2B ; * +
c           =/ %x2D    ; -
c           =/ %x2F-39 ; / 0-9
c           =/ %x3D    ; =
c           =/ %x3F    ; ?
c           =/ %x41-5a ; A-Z
c           =/ %x5E-7E ; ^ _ ` a-z { | } ~
c           =/ %x80-FF ; UTF-8-Octet (not in RFC 2486)
                       ; Where UTF-8-octet is any octet in the
                       ; multi-octet UTF-8 representation of a
                       ; unicode codepoint above %x7F.
                       ; Note that c must also satisfy rules in
                       ; Section 2.4, including, for instance,
                       ; checking that no prohibited output is
                       ; used (see also Section 2.3 of
                       ; [RFC4013]).

                       ; " ( ) , . ; : < > @ [ \ ] DEL	are not allowed

x           =  %x00-FF ; all 128 ASCII characters, no exception;
                       ; as well as all UTF-8-octets as defined
                       ; above (this was not allowed in
                       ; RFC 2486).  Note that x must nevertheless
                       ; again satisfy the Section 2.4 rules.

domain      =  *( label "." ) label
label       =  let-dig *(ldh-str)
ldh-str     =  *( alpha / digit / "-" ) let-dig
let-dig     =  alpha / digit
alpha       =  %x41-5A  ; A-Z
alpha       =/ %x61-7A  ; a-z
digit       =  %x30-39  ; 0-9
